#!/usr/local/bin/bpftrace

#include <linux/fs.h>
#include <linux/mm.h>

// Fits all in one printf to avoid race conditions on outputting
// This trace focuses on reading addresses

BEGIN
{
	printf("%-17s %-6s %-4s %-13s %-8s %s\n", "TIMESTAMP", "PID", "FD", "BUFFER ADDR", "OFFSET", "FILENAME");
}

// ************************************************************
// 	FILE READING
// ************************************************************

// We time the syscall but fetch some extra info from the vfs function

// tracepoint:syscalls:sys_enter_read
//     int __syscall_nr;
//     unsigned int fd;
//     char * buf;
//     size_t count;
tracepoint:syscalls:sys_enter_read
/ comm == "python" /
{
	@read_start[tid] = nsecs;
	@read_fd[tid] = args->fd; 
}
// ssize_t vfs_read(struct file *file, char __user *buf, size_t count, loff_t *pos)
kprobe:vfs_read
/ @read_start[tid] /
{
	@address[tid] = arg1;
	@offset[tid] = *arg3;
	@filename[tid] = str(((struct file *)arg0)->f_path.dentry->d_name.name);
}

// tracepoint:syscalls:sys_exit_read
//     int __syscall_nr;
//     long ret;
tracepoint:syscalls:sys_exit_read
/ @read_start[tid] /
{
	$now = nsecs;
	$latency = $now - @read_start[tid];

	printf("%-17lu %-6d %-4d 0x%-13lx %-8d %s\n", 
		$now, 
		pid,
		@read_fd[tid],
		@address[tid],
		@offset[tid],
		@filename[tid]
	);

	delete(@read_start[tid]);
	delete(@read_fd[tid]);
	delete(@address[tid]);
	delete(@offset[tid]);
	delete(@filename[tid]);
}


// ************************************************************
// 	CLEANUP
// ************************************************************
END 
{
	clear(@read_start);
	clear(@read_fd);
	clear(@address);
	clear(@offset);
	clear(@filename);
}
