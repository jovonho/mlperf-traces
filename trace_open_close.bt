#!/usr/local/bin/bpftrace

#include <linux/fs.h>
#include <linux/mm.h>


BEGIN
{
	// openat() format
	printf("openat():\n")
	printf("%-17s %-10s %-6s %-6s %-10s %-4s ", "TIMESTAMP", "TIME", "PID", "TID", "FUNCTION", "FD");
	printf("%-8s %-8s %s\n", "FLAGS", "LAT(ns)", "FILENAME");

	// vfs_open() format
	printf("vfs_open():\n");
	printf("%-17s %-10s %-6s %-6s %-10s %-8s ", "TIMESTAMP", "TIME", "PID", "TID", "FUNCTION", "LAT(ns)");
	printf("%s\n", "FILENAME");

	// close() format
	printf("close():\n");
	printf("%-17s %-10s %-6s %-6s %-10s %-4s ", "TIMESTAMP", "TIME", "PID", "TID", "FUNCTION", "FD");
	printf("%-8s\n","LAT(ns)");

	// vfs_close() format
	printf("vfs_close():\n");
	printf("%-17s %-10s %-6s %-6s %-10s %-4s ", "TIMESTAMP", "TIME", "PID", "TID", "FUNCTION", "FD");
	printf("%-8s %s\n","LAT(ns)", "FILENAME");
}

// ************************************************************
// 	FILE OPENING
// ************************************************************

// tracepoint:syscalls:sys_enter_openat
//     int __syscall_nr;
//     int dfd;
//     const char * filename;
//     int flags;
//     umode_t mode;
tracepoint:syscalls:sys_enter_openat	
/ comm == "python" /
{
	@o_start[tid] = nsecs;
	@o_flags[tid] = args->flags;
	@o_fn[tid] = str(args->filename);
}
tracepoint:syscalls:sys_exit_openat
/@o_start[tid]/
{
	$now = nsecs;
	$latency = $now - @o_start[tid];
	printf("%-17lu ", $now);
	time("%H:%M:%S  ");
	printf("%-6d %-6d", pid, tid);

	printf("%-10s %-4d %-8x %-8d %s\n",
		"openat()",
		args->ret,			// the file descriptor
		@o_flags[tid],
		$latency,
		@o_fn[tid]
	);

	delete(@o_start[tid]);
	delete(@o_flags[tid]);
	delete(@o_fn[tid]);
}
// int vfs_open(const struct path *path, struct file *file)
kprobe:vfs_open
/comm == "python"/
{
	@vo_start[tid] = nsecs;
	@vo_fn[tid] = str(((struct path *)arg0)->dentry->d_name.name);
}
kretprobe:vfs_open
/@vo_start[tid]/
{
	$now = nsecs;
	$latency = $now - @vo_start[tid];

	printf("%-17lu ", $now);
	time("%H:%M:%S  ");
	printf("%-6d %-6d", pid, tid);

	printf("%-10s %-8d %s\n",
		 "vfs_open()",
		 $latency,
		 @vo_fn[tid]
	 );

	 delete(@vo_start[tid]);
	 delete(@vo_fn[tid]);
}


// ************************************************************
// 	FILE CLOSING
// ************************************************************
// Note: Here we first catch the close() syscall, store the fd then
// add it to the printout of the filp_close() kernel functions.

//tracepoint:syscalls:sys_enter_close
//    int __syscall_nr;
//    unsigned int fd;
tracepoint:syscalls:sys_enter_close
/ comm == "python" /
{	
	@tc_start[tid] = nsecs;
	@closefd[tid] = args->fd;
}
// int filp_close(struct file *filp, fl_owner_t id)
kprobe:filp_close
/ @closefd[tid] /
{
	@c_start[tid] = nsecs;
	@c_fd[tid] = @closefd[tid];
	@c_fn[tid] = str(((struct file *)arg0)->f_path.dentry->d_name.name);
	
}
kretprobe:filp_close
/ @c_start[tid] /
{
	$now = nsecs;
	$latency = $now - @c_start[tid];

	printf("%-17lu ", $now);
	time("%H:%M:%S  ");
	printf("%-6d %-6d", pid, tid);

	printf("%-10s %-8d %-8d %s\n",
		"fp_close()",
		@c_fd[tid],
		$latency,
		@c_fn[tid]
	);

	delete(@c_start[tid]);
	delete(@c_fd[tid]);
	delete(@c_fn[tid]);
}
tracepoint:syscalls:sys_exit_close
/ @tc_start[tid] /
{	
	$now = nsecs;
	$latency = $now - @tc_start[tid];
	
	printf("%-17lu ", $now);
	time("%H:%M:%S  ");
	printf("%-6d %-6d", pid, tid);

	printf("%-10s %-8d %-8d\n", 
		"close()",
		@closefd[tid],
		$latency
	);
	delete(@tc_start[tid]);
	delete(@closefd[tid]);
}


// ************************************************************
// 	CLEANUP
// ************************************************************
END 
{
	clear(@o_start);
	clear(@o_flags);
	clear(@o_fn);

	clear(@vo_start);
	clear(@vo_fn);

	clear(@tc_start);
	clear(@closefd);
	clear(@c_start);
	clear(@c_fd);
	clear(@c_fn);

}
