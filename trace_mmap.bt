#!/usr/local/bin/bpftrace

#include <linux/fs.h>
#include <linux/mm.h>

BEGIN
{
	printf("mmap():\n");
	printf("%-17s %-6s %-4s %-13s %-8s %s\n", "TIMESTAMP", "PID", "FD", "ADDRESS", "LAT(ns)", "FILENAME");

	printf("munmap():\n");
	printf("%-17s %-6s %-13s %-8s %-8s\n", "TIMESTAMP", "PID", "ADDRESS", "LEN", "LAT(ns)");
}

// ************************************************************
// 	MMAP
// ************************************************************
// Had to cut out a lot of output to make it all fit in a single printf call
// Otherwise, under heavy load we would get garbled output, with printf calls interleaving

// tracepoint:syscalls:sys_enter_mmap
//     int __syscall_nr;
//     unsigned long addr;
//     unsigned long len;
//     unsigned long prot;
//     unsigned long flags;
//     unsigned long fd;
//     unsigned long off;
tracepoint:syscalls:sys_enter_mmap
/ comm == "python" /
{
	@mm_start[tid] = nsecs;
	@mm_fd[tid] = args->fd;
	@mm_len[tid] = args->len;
}
//unsigned long do_mmap(struct file *file, unsigned long addr,
//			unsigned long len, unsigned long prot,
//			unsigned long flags, unsigned long pgoff,
//			unsigned long *populate, struct list_head *uf)
kprobe:do_mmap
/ @mm_start[tid] /
{
	$file = (struct file *)arg0;
	@mm_filename[tid] = str($file->f_path.dentry->d_name.name); 
}
// For some reason the return value of the tracepoint doesn't work
// but this retval does.
kretprobe:do_mmap
/ @mm_start[tid] /
{
	@mm_addr[tid] =  retval; 
}
tracepoint:syscalls:sys_exit_mmap
/ @mm_start[tid] /
{
	$now = nsecs;
	$latency = $now - @mm_start[tid];

	printf("%-17lu %-6d %-4d 0x%lx %-8d %s\n", 
		$now,
		pid,
		@mm_fd[tid],
		@mm_addr[tid], 		
		$latency, 
		@mm_filename[tid]
	);

	delete(@mm_start[tid]);
	delete(@mm_len[tid]);
	delete(@mm_fd[tid]);
	delete(@mm_addr[tid]);
}


// ************************************************************
// 	MUNMAP
// ************************************************************
// do_munmap() doesn't have a lot of info so we'll just trace the syscall here

// tracepoint:syscalls:sys_enter_munmap
//     int __syscall_nr;
//     unsigned long addr;
//     size_t len;
tracepoint:syscalls:sys_enter_munmap
/ comm == "python" /
{
	@mun_start[tid] = nsecs;
	@mun_addr[tid] = args->addr;
	@mun_len[tid] = args->len;
}
tracepoint:syscalls:sys_exit_munmap
/ @mun_start[tid] /
{
	$now = nsecs;
	$latency = $now - @mun_start[tid];

	printf("%-17lu %-6d 0x%lx %-8d %-8d\n", 
		$now,
		pid,
		@mun_addr[tid], 
		@mun_len[tid], 
		$latency
	);
	
	delete(@mun_start[tid]);
	delete(@mun_addr[tid]);
	delete(@mun_len[tid]);
}


// ************************************************************
// 	CLEANUP
// ************************************************************
END 
{
	clear(@mm_start);
	clear(@mm_len);
	clear(@mm_fd);
	clear(@mm_addr);

	clear(@mun_start);
	clear(@mun_addr);
	clear(@mun_len);
}
